include(GoogleTest)

# The following line is required to mark ${compiled_resource_file} as generated file.
# Sadly, it's not possible to do that with `set_source_files_properties`, because:
# - using this directory (`DIRECTORY ${CMAKE_CURRENT_SOURCER_DIRECTORY}`), cmake does not find the
#    rule to make the file and fails: the rule exists, but in another directory.
# - using the directory where the rule is defined (`TARGET_DIRECTORY ommpfritt`), cmake does not
#    recognize that the file will be generated in time and complains that the file does not exist.
# The following runs no command when the `${compiled_resource_file}` does not exist.
# The `${compiled_resource_file}` is actually generated by another command, which `libommpfritt`
# depends on. Since the `${TESTNAME}` depends on `libommpfritt`, it's guaranteed that
# `${compiled_source_file}` will always be present when the test is built.
if ("${CMAKE_GENERATOR}" STREQUAL "Ninja")
  set_source_files_properties(${compiled_resource_file} PROPERTIES GENERATED ON)
else()
  add_custom_command(OUTPUT ${compiled_resource_file} COMMENT "No-operation.")
endif()

macro(package_add_test SOURCE_FILE)
    string(REGEX REPLACE "\.[^.]*$" "" TESTNAME "${SOURCE_FILE}")
    add_executable(${TESTNAME} main.cpp testutil.cpp testutil.h ${SOURCE_FILE} ${compiled_resource_file})
    add_dependencies(${TESTNAME} libommpfritt)
    target_link_libraries(${TESTNAME} PRIVATE gtest gmock libommpfritt)
    target_compile_definitions(${TESTNAME} PRIVATE "PYTHON_EXECUTABLE=${PYTHON_EXECUTABLE}")
    target_include_directories(${TESTNAME} PRIVATE ${CMAKE_CURRENT_BINARY_DIR})

    if(BUILD_SHARED_LIBS)
      target_compile_definitions(${TESTNAME} PRIVATE "GTEST_LINKED_AS_SHARED_LIBRARY=1")
    endif()

    gtest_discover_tests(${TESTNAME})
    set_target_properties(${TESTNAME} PROPERTIES FOLDER tests)
endmacro()

package_add_test(color.cpp)
package_add_test(common.cpp)
package_add_test(dnftest.cpp)
package_add_test(geometry.cpp)
package_add_test(iotest.cpp)
package_add_test(nodetest.cpp)
package_add_test(pathtest.cpp)
package_add_test(propertytest.cpp)
package_add_test(splinetypetest.cpp)
package_add_test(transform.cpp)
package_add_test(tree.cpp)
